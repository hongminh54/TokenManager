import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'me.realized'
version '3.2.9-SNAPSHOT'

def spigotApiVersion = (findProperty('spigotApiVersion') ?: '1.8.8-R0.1-SNAPSHOT').toString()

compileJava.options.encoding = 'UTF-8'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"

    filesMatching('**/*.yml') {
        filter(
                ReplaceTokens,
                beginToken: '\${',
                endToken: '}',
                tokens: [
                        project_version: project.version.toString(),
                        NAME           : project.name,
                        VERSION        : project.version.toString(),
                        SHADED_GROUP   : group,
                ],
        )
    }
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/public'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'mvdw-repo'
        url 'http://repo.mvdw-software.be/content/groups/public/'
        allowInsecureProtocol = true
    }

    maven {
        name 'clip-repo'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        allowInsecureProtocol = true
    }

    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.io/repository/maven-public/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }

    maven {
        name = "jitpack"
        url = uri("https://jitpack.io")
    }

    // Sonatype Repository
    maven {
        name = "sonatype"
        url = uri("https://oss.sonatype.org/content/groups/public/")
    }

    maven {
        url = uri("https://maven.enginehub.org/repo/")
    }

    maven {
        name = "minebench-repo"
        url = uri("https://repo.minebench.de/")
    }

    maven {
        name = "MascusJeoraly"
        url = uri("https://raw.github.com/MascusJeoraly/LanguageUtils/mvn-repo/")
    }

    maven {
        url = uri("https://repo.roinujnosde.me/releases/")
    }

    maven {
        name = "essentialsxReleases"
        url = uri("https://repo.essentialsx.net/releases")
    }

    maven {
        url = uri("https://repo.codemc.org/repository/maven-public/")
    }

    flatDir {
        dirs "$rootDir/libs/"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    compileOnly "org.spigotmc:spigot-api:${spigotApiVersion}"
    compileOnly(name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    compileOnly("com.github.MilkBowl:VaultAPI:1.7")
    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'redis.clients:jedis:3.9.0'
    implementation 'org.apache.commons:commons-pool2:2.11.1'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.slf4j:slf4j-nop:1.7.36'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    compileOnly("com.github.hongminh54:Storage:2.3.5-SNAPSHOT")
    implementation("com.github.cryptomorin:XSeries:13.6.0")
    compileOnly("net.luckperms:api:5.5")
    implementation("org.codemc.worldguardwrapper:worldguardwrapper:1.2.1-SNAPSHOT")
}

clean.doFirst {
    delete "$rootDir/out/"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    destinationDirectory = file("$rootDir/out/")
    archiveBaseName = 'TokenManager'
    archiveClassifier = ''

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('com.github.cryptomorin:.*'))
        include(dependency('org.codemc.worldguardwrapper:.*'))

    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
    relocate 'com.github.cryptomorin', group + "xseries"
    relocate 'org.codemc.worldguardwrapper', group + "worldguardwrapper"
}

build.dependsOn(shadowJar)